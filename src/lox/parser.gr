import { forEach } from "list"
import Array from "array"
import Lexer from "./lexer"
import Expr from "./expr"

let mut tokens: Array<Lexer.Token> = [>]
let mut current = 0

let previous = () => {
  tokens[current - 1]
}

let peek = () => {
  tokens[current]
}

let isAtEnd = () => {
  peek().token_type == Lexer.EOF
}

let advance = () => {
  if (!isAtEnd()) {
    current += 1
  }
  previous()
}

let check = t => {
  if (isAtEnd()) {
    false
  } else {
    peek().token_type == t
  }
}

let within = types => {
  let mut ret = false
  forEach(el => {
    if (check(el) && !ret) {
      ret = true
      advance()
      void
    }
  }, types)

  ret
}

//this seems inefficient but I don't know how else to make `expression` callable from the top of the grammar
let rec expression = () => {
  let primary = () => {
    let mut ret = Expr.NilLiteral
    if (within([Lexer.False])) {
      ret = Expr.BoolLiteral(false)
    } else if (within([Lexer.True])) {
      ret = Expr.BoolLiteral(true)
    } else if (within([Lexer.Nil])) {
      ret = Expr.NilLiteral
    } else {
      let t = advance()
      match (t.token_type) {
        Lexer.String(s) => {
          ret = Expr.StringLiteral(s)
        },
        Lexer.Number(n) => {
          ret = Expr.NumLiteral(n)
        },
        _ => {
          if (within([Lexer.LParen])) {
            let e = expression()
            // consume(RParen, "Expect ')' after exression.")
            ret = Expr.Grouping(e)
          }
        },
      }
    }
    ret
  }

  let rec unary = () => {
    if (within([Lexer.Bang, Lexer.Minus])) {
      let op = previous()
      let right = unary()
      Expr.Unary(op, right)
    } else {
      primary()
    }
  }

  let factor = () => {
    let mut e = unary()

    while (within([Lexer.Slash, Lexer.Star])) {
      let op = previous()
      let right = unary()
      e = Expr.Binary(e, op, right)
    }

    e
  }

  let term = () => {
    let mut e = factor()

    while (within([Lexer.Minus, Lexer.Plus])) {
      let op = previous()
      let right = factor()
      e = Expr.Binary(e, op, right)
    }

    e
  }

  let comparison = () => {
    let mut e = term()

    while (
      within([Lexer.Greater, Lexer.GreaterEqual, Lexer.Less, Lexer.LessEqual])
    ) {
      let op = previous()
      let right = term()
      e = Expr.Binary(e, op, right)
    }

    e
  }

  let equality = () => {
    let mut e = comparison()

    while (within([Lexer.BangEqual, Lexer.EqualEqual])) {
      let op = previous()
      let right = comparison()
      e = Expr.Binary(e, op, right)
    }

    e
  }

  equality()
}

export let parse = ts => {
  tokens = ts
  current = 0
  let mut trees = [>]
  while (!isAtEnd()) {
    trees = Array.append(trees, [> expression()])
  }

  trees
}
